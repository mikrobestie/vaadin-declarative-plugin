{
  parserClass = 'cz.mikrobestie.idea.vaadin.declarative.lang.VaadinDesignParser'
  parserUtilClass="cz.mikrobestie.idea.vaadin.declarative.lang.VaadinDesignParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="VD"
  psiImplClassSuffix="Impl"
  psiPackage="cz.mikrobestie.idea.vaadin.declarative.psi"
  psiImplPackage="cz.mikrobestie.idea.vaadin.declarative.psi.impl"

  elementTypeHolderClass="cz.mikrobestie.idea.vaadin.declarative.psi.VDTypes"
  elementTypeClass="cz.mikrobestie.idea.vaadin.declarative.psi.VDElementType"
  tokenTypeClass="cz.mikrobestie.idea.vaadin.declarative.psi.VDTokenType"
}

Document ::= DOCTYPE_DECL? (HtmlTag | Component)
HtmlTag ::= COMMENT* TAG_HTML_OPEN Attr* EL_RIGHT HeadTag? BodyTag TAG_HTML_CLOSE {pin=2}
HeadTag ::= COMMENT* TAG_HEAD_OPEN (MetaTag | COMMENT)* TAG_HEAD_CLOSE {pin = 2}
MetaTag ::= COMMENT* TAG_META_OPEN Attr+ EL_CLOSE_RIGHT {pin = 2}
BodyTag ::= COMMENT* TAG_BODY_OPEN COMMENT* Component COMMENT* TAG_BODY_CLOSE {pin = 2}

Component ::= EL_LEFT ELEM_NAME Attr* (EL_CLOSE_RIGHT | EL_RIGHT (COMMENT | Component)* ComponentClose) {
//    pin             = 2
//    recoverWhile    = "ComponentRecover"
    mixin           = "cz.mikrobestie.idea.vaadin.declarative.psi.impl.VDComponentHelperImpl"
    implements      = "cz.mikrobestie.idea.vaadin.declarative.psi.VDComponentHelper"
    methods         = [getKey getValue getName setName getNameIdentifier]
}
//private ComponentRecover ::= !(EL_LEFT | TAG_BODY_CLOSE)
private ComponentClose ::= EL_CLOSE_LEFT ELEM_NAME EL_RIGHT

Attr ::= ATTR_NAME (EQ ATTR_VALUE)? {
    mixin        = "cz.mikrobestie.idea.vaadin.declarative.psi.impl.VDAttrHelperImpl"
    implements   = "cz.mikrobestie.idea.vaadin.declarative.psi.VDAttrHelper"
}